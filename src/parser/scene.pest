scene = _{SOI ~ flags ~ tilemap ~ EOI}

flags = {(flag ~ ws+)* ~ flag?}
flag = {"-" ~ "-"? ~ flag_name ~ (!"\\" ~ "=" ~ vallist)?}

tilemap = {((row ~ (!"\\" ~ "\n"))* ~ row)?}
row = {((stack ~ (!"\\" ~ " ")+)* ~ stack)?}
stack = {((anim ~ (!"\\" ~ "&"))* ~ anim)?}
anim = {((cell ~ (!"\\" ~ ">"))* ~ cell)?}
cell = {(object ~ variants)?}

object = _{tile}

tile = {tag ~ tile_name}
tag = {text | glyph | ""}
text = {"$"}
glyph = {"#"}

variants = {((!"\\" ~ ":") ~ variant)*}
variant = {var_name ~ ((!"\\" ~ "/") ~ vallist)?}
vallist = _{var_arg ~ ((!"\\" ~ "/") ~ var_arg)*}

flag_name = {value}
flag_arg = {value}
var_name = {value}
var_arg = {value}
tile_name = {value}
value = _{(("\\" | !blacklist) ~ ANY)*}

blacklist = _{ws | "&" | ">" | "=" | ":" | "/"}
ws = _{" " | "\t" | "\n" | "\r"}
